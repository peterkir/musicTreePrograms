/*
 * WARNING
 *
 * This file should not be edited.
 *
 * Add tasks that are relevant to Java projects.
 */

assert(project != rootProject   )
assert(hasProperty('sourceSets'))


apply plugin: 'jacoco'


if (!hasProperty('jacocoToolVersion')) ext.jacocoToolVersion = '0.7.1.201405082137'
if (!hasProperty('jacocoReportsDir' )) ext.jacocoReportsDir  = 'reports/jacoco'    /* relative to build directory  */
if (!hasProperty('jacocoHtmlDir'    )) ext.jacocoHtmlDir     = 'html'              /* relative to jacocoReportsDir */
if (!hasProperty('jacocoXmlReport'  )) ext.jacocoXmlReport   = true
if (!hasProperty('jacocoCsvReport'  )) ext.jacocoCsvReport   = false


jacoco {
  reportsDir  = new File(buildDir, "${jacocoReportsDir}")
  toolVersion = jacocoToolVersion
}

jacocoTestReport {
  dependsOn test
  reports {
      xml.enabled      jacocoXmlReport
      csv.enabled      jacocoCsvReport
      html.destination new File(buildDir, "${jacocoReportsDir}/${jacocoHtmlDir}")
  }
}
